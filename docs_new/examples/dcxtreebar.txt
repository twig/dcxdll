alias xtreebar_callback {
  echo -s treebar: $1 :: $2 :: $3-
  !if ($1 == geticons) {
    ; geticons <type> <text>
    ; returns: normal selected expanded
    !if ($2 == status) return 1 2 3
    !if ($2 == channel) return 4 5 6
    !if ($2 == custom) return 7 8 9
    !if ($2 == send) return 10 11 12
    !if ($2 == get) return 13 14 15
    !if ($2 == notify) return 16 17 18
    !if ($2 == query) return 19 20 21
    !if (*folder iswm $2) return 22 23 24
    !return 0 0 0
  }
  !if ($1 == gettooltip) {
    ; gettooltip <type> <text>
    ; returns: tootlip text
    !if ($2 == channel) {
      !if ($3 == #genscripts) return GenScripts Development!
      !if (#mircscript* iswm $3) return mIRC Scripting Channel
    }
    !if ($2 == status) return $3
    !if ($2 == WindowFolder) return Custom Windows Folder
  }
  !elseif ($1 == setitem) {
    ; setitem <type> <htreeitem> <flags>
    ; no return value.
    echo -s setitem: $2 :: $3 :: $4
  }
}
alias dcxtreebar {
  ; clear any old images from internal list.
  xtreebar -w clear
  ; status
  ; status selected
  xtreebar -w 0 + 0 $mircdiricons\SysWin.icl
  ; channel
  ; channel selected
  xtreebar -w 0 + 1 $mircdiricons\SysWin.icl
  ; custom
  ; custom selected
  xtreebar -w 0 + 5 $mircdiricons\SysWin.icl
  ; dcc send
  ; dcc send selected
  xtreebar -w 0 + 6 $mircdiricons\SysWin.icl
  ; dcc get
  ; dcc get selected
  xtreebar -w 0 + 7 $mircdiricons\SysWin.icl
  ; notify
  ; notify selected
  xtreebar -w 0 + 8 $mircdiricons\SysWin.icl
  ; query
  ; query selected
  xtreebar -w 0 + 3 $mircdiricons\SysWin.icl
  ; folder
  ; folder selected
  xtreebar -w 0 + 15 $mircdiricons\SysWin.icl
  ; load all icons in this file
  ;xtreebar -w 0 + -1 $mircexe
  xtreebar -w 0 + -1 $mircdiricons\maj32.icl
  ; set text colour
  xtreebar -c +t $rgb(255,128,0)
  ; set  line colour
  xtreebar -c +l $rgb(255,0,0)
  ; set message colour
  xtreebar -c +m $rgb(255,0,0)
  ; set event colour
  xtreebar -c +e $rgb(0,0,255)
  ; set xtreebar styles
  xtreebar -s tooltips infotip transparent
  xtreebar -s balloon trackselect nosingleexpand
  ; turn on xtreebar display
  xtreebar -T 1

  ; open custom window just to show treebar doing something.
  window @xtreebar

  ; count how many items are in the treebar
  echo -s cnt: $xtreebar(0).item
  ; show item one
  echo -s item1: $xtreebar(1).item
  ; show item two
  echo -s item2: $xtreebar(2).item
  ; show item three
  echo -s item3: $xtreebar(3).item

}
