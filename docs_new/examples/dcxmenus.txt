;
; see https://vsthemes.org/en/icon/ for icons
;
alias dcxmenus {
  ; NB: The callback alias is ONLY used for special items that have $chr(14) in the item text, these items can trigger without closing the menu.
  mpopup mirc 1 _dcx_mircmenu_callback
  mpopup mircbar 1 _dcx_mircmenu_callback

  ; normal style menu
  ;_dcx_setmenustyle mirc normal
  ;_dcx_setmenustyle mirc darknormal
  ;_dcx_setmenustyle mircbar darknormal

  ; load icons & images used in menus
  var %f = $mircdirimages\dcx_ppac.png
  if ($isfile(%f)) xpopup -i mirc +a 0 %f
  var %f = $mircdiricons\users.ico
  if ($isfile(%f)) xpopup -i mirc + 0 %f
  var %f = $mircdirimages\one.png
  if ($isfile(%f)) xpopup -i mirc +P 0 %f
  var %f = $mircdirCanada.ico
  if ($isfile(%f)) xpopup -i mircbar + 0 %f
  var %f = $mircdiricons\users.ico
  if ($isfile(%f)) xpopup -i mircbar + 0 %f

  ; set selection box as rounded.
  xpopup -R mirc +r 1
  xpopup -R mircbar +r 1

  ; set menu as alpha blended.
  xpopup -R mirc +a 192
  xpopup -R mirc +A 240
  xpopup -R mircbar +a 192
  xpopup -R mircbar +A 240

  ; enable tooltips
  xpopup -R mirc +t 1
  xpopup -R mircbar +t 1

  ;
  _setupmircmenubar

  _dcx_setdarkmode

  ;
  ;
}
; $1 = cmd id
alias _mircmenubar_callback {
  echo -s _mircmenubar_callback: $1-
  ; dont allow View->Menu to kill menubar as we have a custom item added use /xmenubar -p +v 0 instead as this preserves our custom addon.
  if ($1 == 110) return $true
}
alias _setupmircmenubar {
  var %m = mircmenubar_switch
  if ($xpopup(%m).ismenu) {
    if ($xmenubar(%m).ismenu) xmenubar -d %m
    xpopup -d %m
  }
  xpopup -c %m normal $chr(9) _mircmenubar_callback
  xpop -a %m -1 $chr(9) + 1 2 Add Browser Tab $chr(9) Open new Browser tab.

  _dcx_setmenustyle %m darknormal

  xmenubar -M _mircmenubar_callback
  xmenubar -a %m >|

  xmenubar -p +S normal
  xmenubar -p +HowWbtTBh $rgb(188,188,188) $rgb(188,188,188) $rgb(188,188,188) $color(88) $rgb(54,54,54) $rgb(54,54,54) 0 $rgb(19,19,19) $rgb(19,19,19)
  xmenubar -p +O 0
  xmenubar -p +e 1
}
; changes the menu to a random style.
; $1 = menu
alias _dcx_randommenustyle {
  echo -s menu: $xpopup($1).isopen
  if (!$xpopup($1).ismenu) return
  if (!$xpopup($1).isopen) return
  var %t = normal darknormal vertical verticalrev button buttonrev grade graderev custom custombig officexp office2003 office2003rev icy icyrev
  _dcx_setmenustyle $1 $gettok(%t,$r(1,$numtok(%t,32)),32)
  xpopup -r $1
  .timermenu 1 3 _dcx_randommenustyle $1
}
; $1 = menu name, $2 = style name
alias _dcx_setmenustyle {
  if ($istok(normal darknormal vertical verticalrev button buttonrev grade graderev custom custombig officexp office2003 office2003rev icy icyrev,$2,32)) {
    xpopup -l $1 1-20 default

    if ($2 == darknormal) xpopup -t $1 normal
    else xpopup -t $1 $2

    if ($2 == normal) {
      set $+(%,dcx_darkmode_,$1) 0
      xpopup -l $1 1 $rgb(240,240,240)
      xpopup -l $1 3,7 $color(83)
      xpopup -l $1 8,13 $color(59)
      xpopup -l $1 12 $color(60)
    }
    elseif ($2 == darknormal) {
      set $+(%,dcx_darkmode_,$1) 1

      xpopup -l $1 1,3,13 $rgb(54,54,54) ;color(91)
      xpopup -l $1 4 $rgb(19,19,19) ;color(89)
      xpopup -l $1 6,7,9,19,20 $rgb(77,77,77) ;color(92)
      xpopup -l $1 8,10,11,12 $rgb(188,188,188) ;color(96)
    }
    elseif ($2 == button) xpopup -l $1 7 $rgb(13,180,242)
    elseif ($2 == buttonrev) xpopup -l $1 7 $rgb(13,180,242)
    elseif ($2 == grade) xpopup -l $1 1 $rgb(255,192,34)
    elseif ($2 == custom) {
      var %f = $mircdirtop_bg.jpg
      if ($isfile(%f)) xpopup -b $1 %f
    }
    elseif ($2 == custombig) {
      var %f = $mircdirtop_bg.jpg
      if ($isfile(%f)) xpopup -b $1 %f
      ; NB: this colour is only seen when alpha draw code is enabled in DrawMenuBitmap() when compiling dll.
      xpopup -l $1 1 $rgb(255,192,34)
    }

    if ($xpopup($1).isroundedmenu) {
      ; set border colour to match background colour
      xpopup -l $1 19 $xpopup($1,1).color
    }
  }
  elseif ($2 == rounded) xpopup -R $1 +r 1
  elseif ($2 == square) xpopup -R $1 +r 0
  elseif ($2 == roundedmenu) xpopup -R $1 +R 1
  elseif ($2 == squaremenu) xpopup -R $1 +R 0
  elseif ($2 == tooltips) xpopup -R $1 +t 1
  elseif ($2 == notooltips) xpopup -R $1 +t 0
  elseif ($2 == alpha) xpopup -R $1 +a 192
  elseif ($2 == noalpha) xpopup -R $1 +a 255
  elseif ($2 == vsep) xpopup -x $1 $xpopup($1).exstyle $+ v
  elseif ($2 == novsep) xpopup -x $1 $remove($xpopup($1).exstyle,v)
  elseif ($2 == doublesep) xpopup -x $1 $xpopup($1).exstyle $+ D
  elseif ($2 == nodoublesep) xpopup -x $1 $remove($xpopup($1).exstyle,D)

  if ($1 == mircbar) .timer 1 0 _dcx_setmenustyle scriptpopup $2
}
; $1 = menu name, $2 = style name
alias -l _dcx_ismenustyle {
  ;return $iif($xpopup($1).style == $2,1,0)

  var %s = $($+(%,dcx_menustyle_,$1),2)
  if (!%s) {
    var %s = $xpopup($1).style
    if ((%s == normal) && ($($+(%,dcx_darkmode_,$1),2))) var %s = darknormal
    set -u5 $+(%,dcx_menustyle_,$1) %s
  }
  return $iif(%s == $2,1,0)
}
; $1 = menuname, $2 = id, $3 = cmd
alias _dcx_mircmenu_callback {
  ; changes made to the menu here are done while the menu is still open.
  if ($3 == checksel) {
    if ($2 isnum 600-615) {
      if ($2 == 600) _dcx_setmenustyle mircbar normal
      elseif ($2 == 601) _dcx_setmenustyle mircbar darknormal
      elseif ($2 == 602) _dcx_setmenustyle mircbar vertical
      elseif ($2 == 603) _dcx_setmenustyle mircbar verticalrev
      elseif ($2 == 604) _dcx_setmenustyle mircbar button
      elseif ($2 == 605) _dcx_setmenustyle mircbar buttonrev
      elseif ($2 == 606) _dcx_setmenustyle mircbar grade
      elseif ($2 == 607) _dcx_setmenustyle mircbar graderev
      elseif ($2 == 608) _dcx_setmenustyle mircbar custom
      elseif ($2 == 609) _dcx_setmenustyle mircbar custombig
      elseif ($2 == 610) _dcx_setmenustyle mircbar officexp
      elseif ($2 == 611) _dcx_setmenustyle mircbar office2003
      elseif ($2 == 612) _dcx_setmenustyle mircbar office2003rev
      elseif ($2 == 613) _dcx_setmenustyle mircbar icy
      elseif ($2 == 614) _dcx_setmenustyle mircbar icyrev
      elseif ($2 == 615) _dcx_randommenustyle mircbar

      if ($1 == mircbar) xpopup -r $1
      return check 1 16
    }
    ; 616-619 sets additional effects
    elseif ($2 isnum 616-621) {
      if ($2 == 616) {
        _dcx_setmenustyle mircbar $iif($xpopup(mircbar).isrounded,square,rounded)
        if ($1 == mircbar) xpopup -r $1
        return $iif($xpopup(mircbar).isrounded,check,uncheck)
      }
      elseif ($2 == 617) {
        _dcx_setmenustyle mircbar $iif($xpopup(mircbar).isroundedmenu,squaremenu,roundedmenu)
        if ($1 == mircbar) xpopup -r $1
        return $iif($xpopup(mircbar).isroundedmenu,check,uncheck)
      }
      elseif ($2 == 618) {
        _dcx_setmenustyle mircbar $iif($xpopup(mircbar).istooltips,notooltips,tooltips)
        if ($1 == mircbar) xpopup -r $1
        return $iif($xpopup(mircbar).istooltips,check,uncheck)
      }
      elseif ($2 == 619) {
        ; doesnt work correctly atm. change is only applied when menu reopens.
        _dcx_setmenustyle mircbar $iif($xpopup(mircbar).alpha != 255,noalpha,alpha)
        if ($1 == mircbar) xpopup -r $1
        return $iif($xpopup(mircbar).alpha == 255,uncheck,check)
      }
      elseif ($2 == 620) {
        _dcx_setmenustyle mircbar $iif(v isincs $xpopup(mircbar).exstyle,novsep,vsep)
        if ($1 == mircbar) xpopup -r $1
        return $iif(v !isincs $xpopup(mircbar).exstyle,uncheck,check)
      }
      elseif ($2 == 621) {
        _dcx_setmenustyle mirc $iif(D isincs $xpopup(mirc).exstyle,nodoublesep,doublesep)
        if ($1 == mirc) xpopup -r $1
        return $iif(D !isincs $xpopup(mirc).exstyle,uncheck,check)
      }
    }
    elseif ($2 isnum 700-715) {
      if ($2 == 700) _dcx_setmenustyle mirc normal
      elseif ($2 == 701) _dcx_setmenustyle mirc darknormal
      elseif ($2 == 702) _dcx_setmenustyle mirc vertical
      elseif ($2 == 703) _dcx_setmenustyle mirc verticalrev
      elseif ($2 == 704) _dcx_setmenustyle mirc button
      elseif ($2 == 705) _dcx_setmenustyle mirc buttonrev
      elseif ($2 == 706) _dcx_setmenustyle mirc grade
      elseif ($2 == 707) _dcx_setmenustyle mirc graderev
      elseif ($2 == 708) _dcx_setmenustyle mirc custom
      elseif ($2 == 709) _dcx_setmenustyle mirc custombig
      elseif ($2 == 710) _dcx_setmenustyle mirc officexp
      elseif ($2 == 711) _dcx_setmenustyle mirc office2003
      elseif ($2 == 712) _dcx_setmenustyle mirc office2003rev
      elseif ($2 == 713) _dcx_setmenustyle mirc icy
      elseif ($2 == 714) _dcx_setmenustyle mirc icyrev
      elseif ($2 == 715) _dcx_randommenustyle mirc

      if ($1 == mirc) xpopup -r $1
      return check 1 16
    }
    ; 716-720 sets additional effects
    elseif ($2 isnum 716-721) {
      if ($2 == 716) {
        _dcx_setmenustyle mirc $iif($xpopup(mirc).isrounded,square,rounded)
        if ($1 == mirc) xpopup -r $1
        return $iif($xpopup(mirc).isrounded,check,uncheck)
      }
      elseif ($2 == 717) {
        _dcx_setmenustyle mirc $iif($xpopup(mirc).isroundedmenu,squaremenu,roundedmenu)
        if ($1 == mirc) xpopup -r $1
        return $iif($xpopup(mirc).isroundedmenu,check,uncheck)
      }
      elseif ($2 == 718) {
        _dcx_setmenustyle mirc $iif($xpopup(mirc).istooltips,notooltips,tooltips)
        if ($1 == mirc) xpopup -r $1
        return $iif($xpopup(mirc).istooltips,check,uncheck)
      }
      elseif ($2 == 719) {
        ; doesnt work correctly atm. change is only applied when menu reopens.
        _dcx_setmenustyle mirc $iif($xpopup(mirc).alpha != 255,noalpha,alpha)
        if ($1 == mirc) xpopup -r $1
        return $iif($xpopup(mirc).alpha == 255,uncheck,check)
      }
      elseif ($2 == 720) {
        _dcx_setmenustyle mirc $iif(v isincs $xpopup(mirc).exstyle,novsep,vsep)
        if ($1 == mirc) xpopup -r $1
        return $iif(v !isincs $xpopup(mirc).exstyle,uncheck,check)
      }
      elseif ($2 == 721) {
        _dcx_setmenustyle mirc $iif(D isincs $xpopup(mirc).exstyle,nodoublesep,doublesep)
        if ($1 == mirc) xpopup -r $1
        return $iif(D !isincs $xpopup(mirc).exstyle,uncheck,check)
      }
    }
  }
}
alias -l _dcx_setdarkmode {
  _dcx_setmenustyle mircbar darknormal
  _dcx_setmenustyle mirc darknormal
  _dcx_setmenustyle mircmenubar_switch darknormal

  xmenubar -p +e 1
  set %dcx_darkmode_enabled 1
  set %dcx_darkmode_bkgdark_colour $color(88)
  set %dcx_darkmode_bkglight_colour $rgb(54,54,54)
  set %dcx_darkmode_txt_colour $rgb(188,188,188)
  var %f = $qt($mircdirdarkmodebkg.bmp)
  if (!$isfile(%f)) {
    if ($window(@bkgtmp)) window -c $v1
    window -hp +d @bkgtmp 0 0 100 10
    drawfill -r @bkgtmp %dcx_darkmode_bkgdark_colour 0 0 0
    drawsave @bkgtmp %f
    window -c @bkgtmp
  }
  background -lr %f
  background -br %f
  background -hr %f
  color treebar 95
  color treebar text 97
}
alias -l _dcx_setlightmode {
  _dcx_setmenustyle mircbar normal
  _dcx_setmenustyle mirc normal
  _dcx_setmenustyle mircmenubar_switch normal

  xmenubar -p +e 0
  set %dcx_darkmode_enabled 0
  background -lx
  background -bx
  background -hx
  color treebar 0
  color treebar text 1
}
menu status,menubar {
  DCX
  .Unload DCX:udcx
  .Menu Item Types
  ..$xstyle(11,button) 88Button $xstyle(9,Button style item): noop
  ..$xstyle(11,progress) 88Progress $xstyle(9,Progressbar style item): noop
  ..$xstyle(11,track) 88Track $xstyle(9,Trackbar style item): noop
  ..$xstyle(11,grade) 88Gradient $xstyle(9,Gradient style item): noop
  ..$xstyle(11,custom) 88Custom $xstyle(9,Custom style item): noop
  ..$xstyle(11,icy) 88Icy $xstyle(9,Icy style item): noop
  ..$xstyle(11,Normal) 88Normal $xstyle(9,Normal style item): noop
  .Settings
  ..Set Dark Mode: _dcx_setdarkmode
  ..Set Light Mode: _dcx_setlightmode
  ..Menu Options
  ...Menubar
  ....Menu Styles
  .....$iif($_dcx_ismenustyle(mircbar,normal),$style(1)) $xstyle(12) Normal $xstyle(14,600) $xstyle(9,Normal looking menu): _dcx_setmenustyle mircbar normal
  .....$iif($_dcx_ismenustyle(mircbar,darknormal),$style(1)) $xstyle(12) Dark Normal $xstyle(14,601) $xstyle(9,Dark Normal looking menu): _dcx_setmenustyle mircbar darknormal
  .....$iif($_dcx_ismenustyle(mircbar,vertical),$style(1)) $xstyle(12) Vertical $xstyle(14,602) $xstyle(9,Vertical Gradient): _dcx_setmenustyle mircbar vertical
  .....$iif($_dcx_ismenustyle(mircbar,verticalrev),$style(1)) $xstyle(12) VerticalRev $xstyle(14,603) $xstyle(9,Reverse Vertical Gradient): _dcx_setmenustyle mircbar verticalrev
  .....$iif($_dcx_ismenustyle(mircbar,button),$style(1)) $xstyle(12) Button $xstyle(14,604) $xstyle(9,Items are buttons): _dcx_setmenustyle mircbar button
  .....$iif($_dcx_ismenustyle(mircbar,buttonrev),$style(1)) $xstyle(12) ButtonRev $xstyle(14,605) $xstyle(9,Items are sunken buttons): _dcx_setmenustyle mircbar buttonrev
  .....$iif($_dcx_ismenustyle(mircbar,grade),$style(1)) $xstyle(12) Gradient $xstyle(14,606) $xstyle(9,Gradient): _dcx_setmenustyle mircbar grade
  .....$iif($_dcx_ismenustyle(mircbar,graderev),$style(1)) $xstyle(12) GradientRev $xstyle(14,607) $xstyle(9,Reverse Gradient): _dcx_setmenustyle mircbar graderev
  .....$iif($_dcx_ismenustyle(mircbar,custom),$style(1)) $xstyle(12) Custom $xstyle(14,608) $xstyle(9,Each item is a custom bitmap): _dcx_setmenustyle mircbar custom
  .....$iif($_dcx_ismenustyle(mircbar,custombig),$style(1)) $xstyle(12) CustomBig $xstyle(14,609) $xstyle(9,Custom bitmap covers whole menu): _dcx_setmenustyle mircbar custombig
  .....$iif($_dcx_ismenustyle(mircbar,officexp),$style(1)) $xstyle(12) OfficeXP $xstyle(14,610) $xstyle(9,OfficeXP style): _dcx_setmenustyle mircbar officexp
  .....$iif($_dcx_ismenustyle(mircbar,office2003),$style(1)) $xstyle(12) Office2003 $xstyle(14,611) $xstyle(9,Office2003 style): _dcx_setmenustyle mircbar office2003
  .....$iif($_dcx_ismenustyle(mircbar,office2003rev),$style(1)) $xstyle(12) Office2003rev $xstyle(14,612) $xstyle(9,Reverse Office2003 style): _dcx_setmenustyle mircbar office2003rev
  .....$iif($_dcx_ismenustyle(mircbar,icy),$style(1)) $xstyle(12) Icy $xstyle(14,613) $xstyle(9,Ice effect): _dcx_setmenustyle mircbar icy
  .....$iif($_dcx_ismenustyle(mircbar,icyrev),$style(1)) $xstyle(12) IcyRev $xstyle(14,614) $xstyle(9,Reverse Ice effect): _dcx_setmenustyle mircbar icyrev
  .....Randomize $xstyle(14,615): _dcx_randommenustyle mircbar
  ....$iif($xpopup(mircbar).isrounded,$style(1)) Rounded Selector $xstyle(14,616) $xstyle(9,Makes menu selection a rounded rect): _dcx_setmenustyle mircbar $iif($xpopup(mircbar).isrounded,square,rounded)
  ....$iif($xpopup(mircbar).isroundedmenu,$style(1)) Rounded Window $xstyle(14,617) $xstyle(9,Make menus rounded rects): _dcx_setmenustyle mircbar $iif($xpopup(mircbar).isroundedmenu,squaremenu,roundedmenu)
  ....$iif($xpopup(mircbar).istooltips,$style(1)) Tooltips $xstyle(14,618) $xstyle(9,Enable tooltips like this one!): _dcx_setmenustyle mircbar $iif($xpopup(mircbar).istooltips,notooltips,tooltips)
  ....$iif($xpopup(mircbar).alpha != 255,$style(1)) Alpha Blend $xstyle(14,619) $xstyle(9,Set the menus to be translucent): _dcx_setmenustyle mircbar $iif($xpopup(mircbar).alpha != 255,noalpha,alpha)
  ....$iif(v isincs $xpopup(mircbar).exstyle,$style(1)) Vertical Sep $xstyle(14,620) $xstyle(9,Draw a vertical seperator): _dcx_setmenustyle mircbar $iif(v isincs $xpopup(mircbar).exstyle,novsep,vsep)
  ....$iif(D isincs $xpopup(mircbar).exstyle,$style(1)) Double Sep $xstyle(14,621) $xstyle(9,Draw a double seperator): _dcx_setmenustyle mircbar $iif(D isincs $xpopup(mircbar).exstyle,nodoublesep,doublesep)
  ...Popups
  ....Menu Styles
  .....$iif($_dcx_ismenustyle(mirc,normal),$style(1)) $xstyle(12) Normal $xstyle(14,700) $xstyle(9,Normal looking menu): _dcx_setmenustyle mirc normal
  .....$iif($_dcx_ismenustyle(mirc,darknormal),$style(1)) $xstyle(12) Dark Normal $xstyle(14,701) $xstyle(9,Dark Normal looking menu): _dcx_setmenustyle mirc darknormal
  .....$iif($_dcx_ismenustyle(mirc,vertical),$style(1)) $xstyle(12) Vertical $xstyle(14,702) $xstyle(9,Vertical Gradient): _dcx_setmenustyle mirc vertical
  .....$iif($_dcx_ismenustyle(mirc,verticalrev),$style(1)) $xstyle(12) VerticalRev $xstyle(14,703) $xstyle(9,Reverse Vertical Gradient): _dcx_setmenustyle mirc verticalrev
  .....$iif($_dcx_ismenustyle(mirc,button),$style(1)) $xstyle(12) Button $xstyle(14,704) $xstyle(9,Items are buttons): _dcx_setmenustyle mirc button
  .....$iif($_dcx_ismenustyle(mirc,buttonrev),$style(1)) $xstyle(12) ButtonRev $xstyle(14,705) $xstyle(9,Items are sunken buttons): _dcx_setmenustyle mirc buttonrev
  .....$iif($_dcx_ismenustyle(mirc,grade),$style(1)) $xstyle(12) Gradient $xstyle(14,706) $xstyle(9,Gradient): _dcx_setmenustyle mirc grade
  .....$iif($_dcx_ismenustyle(mirc,graderev),$style(1)) $xstyle(12) GradientRev $xstyle(14,707) $xstyle(9,Reverse Gradient): _dcx_setmenustyle mirc graderev
  .....$iif($_dcx_ismenustyle(mirc,custom),$style(1)) $xstyle(12) Custom $xstyle(14,708) $xstyle(9,Each item is a custom bitmap): _dcx_setmenustyle mirc custom
  .....$iif($_dcx_ismenustyle(mirc,custombig),$style(1)) $xstyle(12) CustomBig $xstyle(14,709) $xstyle(9,Custom bitmap covers whole menu): _dcx_setmenustyle mirc custombig
  .....$iif($_dcx_ismenustyle(mirc,officexp),$style(1)) $xstyle(12) OfficeXP $xstyle(14,710) $xstyle(9,OfficeXP style): _dcx_setmenustyle mirc officexp
  .....$iif($_dcx_ismenustyle(mirc,office2003),$style(1)) $xstyle(12) Office2003 $xstyle(14,711) $xstyle(9,Office2003 style): _dcx_setmenustyle mirc office2003
  .....$iif($_dcx_ismenustyle(mirc,office2003rev),$style(1)) $xstyle(12) Office2003rev $xstyle(14,712) $xstyle(9,Reverse Office2003 style): _dcx_setmenustyle mirc office2003rev
  .....$iif($_dcx_ismenustyle(mirc,icy),$style(1)) $xstyle(12) Icy $xstyle(14,713) $xstyle(9,Ice effect): _dcx_setmenustyle mirc icy
  .....$iif($_dcx_ismenustyle(mirc,icyrev),$style(1)) $xstyle(12) IcyRev $xstyle(14,714) $xstyle(9,Reverse Ice effect): _dcx_setmenustyle mirc icyrev
  .....Randomize $xstyle(14,715): _dcx_randommenustyle mirc
  ....$iif($xpopup(mirc).isrounded,$style(1)) Rounded Selector $xstyle(14,716) $xstyle(9,Makes menu selection a rounded rect): _dcx_setmenustyle mirc $iif($xpopup(mirc).isrounded,square,rounded)
  ....$iif($xpopup(mirc).isroundedmenu,$style(1)) Rounded Window $xstyle(14,717) $xstyle(9,Make menus rounded rects): _dcx_setmenustyle mirc $iif($xpopup(mirc).isroundedmenu,squaremenu,roundedmenu)
  ....$iif($xpopup(mirc).istooltips,$style(1)) Tooltips $xstyle(14,718) $xstyle(9,Enable tooltips like this one!): _dcx_setmenustyle mirc $iif($xpopup(mirc).istooltips,notooltips,tooltips)
  ....$iif($xpopup(mirc).alpha != 255,$style(1)) Alpha Blend $xstyle(14,719) $xstyle(9,Set the menus to be translucent): _dcx_setmenustyle mirc $iif($xpopup(mirc).alpha != 255,noalpha,alpha)
  ....$iif(v isincs $xpopup(mirc).exstyle,$style(1)) Vertical Sep $xstyle(14,720) $xstyle(9,Draw a vertical seperator): _dcx_setmenustyle mirc $iif(v isincs $xpopup(mirc).exstyle,novsep,vsep)
  ....$iif(D isincs $xpopup(mirc).exstyle,$style(1)) Double Sep $xstyle(14,721) $xstyle(9,Draw a double seperator): _dcx_setmenustyle mirc $iif(D isincs $xpopup(mirc).exstyle,nodoublesep,doublesep)
  ...$iif($dcx(GetDCXSettings,StaticColours),$style(1)) Static Colours $xstyle(9,Fix the colours used by DCX (faster)): {
    if ($dcx(GetDCXSettings,StaticColours)) dcx SetDCXSettings StaticColours 0
    else dcx SetDCXSettings StaticColours 1
  }
  ...UpdateColours $xstyle(9,Force DCX to update the colours to match mIRC's): dcx SetDCXSettings UpdateColours
}
