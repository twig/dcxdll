
; set %dcx_browser_home to whatever you want the home url to be.

; menus for IE & WebView2 versions.
menu status,menubar {
  DCX
  .Scripts
  ..WebBrowser IE: dcx_webbrowser IE
  ..WebBrowser WebView2: dcx_webbrowser WebView2
}

; this alias opens the dialog
; $1 = browser type
alias dcx_webbrowser {
  ; if dialog already exists, then show it.
  if ($dialog(dcx_webbrowser)) dialog -v dcx_webbrowser
  else {
    ; default to IE browser type
    if ($0) {
      set %dcx_browser_default_type webctrl
      if ($1 == WebView2) set %dcx_browser_default_type web2ctrl
    }
    set %dcx_browser_type %dcx_browser_default_type
    ; open dialog
    dialog -m dcx_webbrowser dcx_webbrowser
  }
}

; simple dialog as our base.
dialog dcx_webbrowser {
  title "DCX Tabbed WebBrowser"
  icon $mircexe,0
  option pixels
  size -1 -1 500 400

  menu "&File", 60
  item "&Load", 800, 60
  item break, 100
  item "&Save", 801
  item "&Save As...", 802
  item "Capture", 803
  item break, 180
  item "&Cancel", 200, cancel

  menu "Options", 61
  menu "Default Browser", 62, 61
  item "IE $chr(9) Set Internet Explorer as the default.", 311
  item "WebView2 $chr(9) Set WebView2 as the default.", 312
  menu "Menu Options", 68, 61
  menu "Menu Styles", 67, 68
  item "Normal $chr(9) Normal looking menu", 341, 67
  item "Vertical $chr(9) Vertical Gradient", 342, 67
  item "VerticalRev $chr(9) Reverse Vertical Gradient", 343, 67
  item "Button $chr(9) Items are buttons", 344, 67
  item "ButtonRev $chr(9) Items are sunken buttons", 345, 67
  item "Gradient $chr(9) Gradient", 346, 67
  item "GradientRev $chr(9) Reverse Gradient", 347, 67
  item "Custom $chr(9) Each item is a custom bitmap", 348, 67
  item "CustomBig $chr(9) Custom bitmap covers whole menu", 349, 67
  item "OfficeXP $chr(9) OfficeXP style", 350, 67
  item "Office2003 $chr(9) Office2003 style", 351, 67
  item "Office2003rev $chr(9) Reverse Office2003 style", 352, 67
  item "Icy $chr(9) Ice effect", 353, 67
  item "IcyRev $chr(9) Reverse Ice effect", 354, 67

  item "Rounded Selector $chr(9) Makes menu selection a rounded rect", 355, 68
  item "Rounded Window $chr(9) Make menus rounded rects", 356, 68
  item "Square Selector $chr(9) Makes menu selection a rect", 357, 68
  item "Square Window $chr(9) Make menus rects", 358, 68
  item "Tooltips $chr(9) Enable tooltips like this one!", 359, 68
  item "No Tooltips $chr(9) Disable tooltips like this one!", 360, 68
  item "Alpha Blend $chr(9) Set the menus to be translucent", 361, 68
  item "No Alpha Blend $chr(9) disable alpha blending", 362, 68
  item "Hide Menubar $chr(9) Hide windows menubar.", 33, 61
}

; this alias updates the CLA and drawing after the init event.
alias dcx_webbrowser_postinit {
  xdialog -l $1 update
  xdialog -j $1
}

; change the url displayed on the specified tab.
; $1 = dname, $2 = tab id, $3- = url
alias dcx_webbrowser_newurl {
  var %id = $2, %url = $3-, %edit_id = $+(%id,_edit), %web_id = $+(%id,_web)

  xdid -ra $1 %edit_id %url
  xdid -m $1 %web_id +usm +um %url
  ;xdid -m $1 %web_id + + %url
}

; go home
; $1 = dname, $2 = tab id
alias dcx_webbrowser_gohome {
  var %url = $iif(%dcx_browser_home,$v1,about:blank)

  dcx_webbrowser_newurl $1 $2 %url
}

; adjust the position of the add tab button
; $1 = dname
alias dcx_webbrowser_positionaddbutton {
  var %pos = $xdid($1,5).pos, %tabsrect = $xdid($1,4).tabsrect
  xdid -p $1 5 $gettok(%tabsrect,3,32) $gettok(%pos,2-,32)
  xdid -s $1 5
}

; remove the specified tab
; $1 = dname, $2- = tabnum
alias dcx_webbrowser_remtab {
  ; only remove the tab if > 2 tabs exist.
  if ($xdid($1,4).num < 2) return
  xdid -d $1 4 $2
  ; update button position after removal.
  dcx_webbrowser_positionaddbutton $1
}

; add a browser toolbar
; $1 = dname, $2 = tab id
alias dcx_webbrowser_addtoolbar {
  var %id = $2, %rebar_id = $+(%id,_rebar), %edit_id = $+(%id,_edit), %web_id = $+(%id,_web), %back_id = $+(%id,_back), %forward_id = $+(%id,_forward), %refresh_id = $+(%id,_refresh), %home_id = $+(%id,_home), %favicon_id = $+(%id,_favicon)
  var %security_id = $+(%id,_security)

  ; the toolbar as seperate controls
  xdid -c $1 %id %back_id button 0 0 0 0 noformat tooltips disabled tabstop
  xdid -t $1 %back_id Back
  xdid -T $1 %back_id Go backwards...

  xdid -c $1 %id %forward_id button 0 0 0 0 noformat tooltips disabled tabstop
  xdid -t $1 %forward_id Forward
  xdid -T $1 %forward_id Go forwards...

  xdid -c $1 %id %refresh_id button 0 0 0 0 noformat tooltips tabstop
  xdid -t $1 %refresh_id Refresh
  xdid -T $1 %refresh_id Refresh page...

  xdid -c $1 %id %home_id button 0 0 0 0 noformat tooltips tabstop
  xdid -t $1 %home_id Home
  xdid -T $1 %home_id Go home...

  xdid -c $1 %id %favicon_id image 0 0 0 0 transparent hidden

  xdid -c $1 %id %security_id image 0 0 0 0 transparent hidden
  ; This provides a padlock icon.
  var %f = $mircdirIcons\Vista\vista32.icl
  if ($isfile(%f)) xdid -w $1 %security_id + 80 20 %f

  xdid -c $1 %id %edit_id edit 0 0 0 0 autohs tabstop
  xdid -E $1 %edit_id + -es

  xdid -l $1 %id cell 1 $chr(9) +fiwvh %back_id 1 40 20
  xdid -l $1 %id cell 1 $chr(9) +fiwvh %forward_id 1 50 20
  xdid -l $1 %id cell 1 $chr(9) +fiwvh %refresh_id 1 50 20
  xdid -l $1 %id cell 1 $chr(9) +fiwvh %home_id 1 40 20
  xdid -l $1 %id cell 1 $chr(9) +fiwvh %favicon_id 1 20 20
  xdid -l $1 %id cell 1 $chr(9) +fiwvh %security_id 1 20 20
  xdid -l $1 %id cell 1 $chr(9) +fiwv %edit_id 1 0 20

  ; the toolbar as a rebar control
  ;xdid -c $1 %id %rebar_id rebar 0 0 0 0 tooltips tabstop noauto dlbclktoggle fixedorder
  ;;[DNAME] [ID] [N] [+FLAGS] [CX] [CY] [WIDTH] [ICON] [COLOR] (TEXT) [TAB] [CID] [CONTROL] [X] [Y] [W] [H] (OPTIONS) [TAB] (TOOLTIP)
  ;xdid -a $1 %rebar_id 1 +f 40 20 40 0 0 $chr(9) %back_id button 0 0 0 0 noformat tooltips disabled tabstop $chr(9) Go Backwards...
  ;xdid -t $1 %back_id Back
  ;xdid -T $1 %back_id Go Backwards...
  ;xdid -a $1 %rebar_id 1 +f 50 20 50 0 0 $chr(9) %forward_id button 0 0 0 0 noformat tooltips disabled tabstop $chr(9) Go Forwards...
  ;xdid -t $1 %forward_id Forward
  ;xdid -T $1 %forward_id Go forwards...
  ;xdid -a $1 %rebar_id 3 +f 50 20 50 0 0 $chr(9) %refresh_id button 0 0 0 0 noformat tooltips disabled tabstop $chr(9) Refresh Page...
  ;xdid -t $1 %refresh_id Refresh
  ;xdid -T $1 %refresh_id Refresh page...
  ;xdid -a $1 %rebar_id 4 +f 40 20 40 0 0 $chr(9) %home_id button 0 0 0 0 noformat tooltips tabstop $chr(9) Go Home...
  ;xdid -t $1 %home_id Home
  ;xdid -T $1 %home_id Go home...
  ;xdid -a $1 %rebar_id 5 +f 20 20 20 0 0 $chr(9) %favicon_id image 0 0 0 0 transparent $chr(9)
  ;xdid -a $1 %rebar_id 5 +f 20 20 20 0 0 $chr(9) %security_id image 0 0 0 0 transparent $chr(9)
  ;xdid -a $1 %rebar_id 6 +n 80 20 80 0 0 $chr(9) %edit_id edit 0 0 0 0 autohs tabstop $chr(9)
  ;xdid -v $1 %rebar_id 1 2
  ;xdid -l $1 %id cell 1 $chr(9) +fiwv %rebar_id 1 0 20
  ; This provides a padlock icon.
  ;var %f = $mircdirIcons\Vista\vista32.icl
  ;if ($isfile(%f)) xdid -w $1 %security_id + 80 20 %f
}

; add a browser tab
; $1 = dname, $2- = url if any
alias dcx_webbrowser_addtab {
  ; increase the tab coun t to make sure we have a unique id
  inc %dcx_browser_tabcnt

  ; create the named id's for this tab
  var %id = $+(Tab,%dcx_browser_tabcnt), %url = $iif($2 != $null,$2,about:blank)
  var %edit_id = $+(%id,_edit), %web_id = $+(%id,_web), %back_id = $+(%id,_back), %forward_id = $+(%id,_forward), %refresh_id = $+(%id,_refresh), %home_id = $+(%id,_home), %favicon_id = $+(%id,_favicon)

  ; make sure browser type is set.
  if (!$istok(webctrl web2ctrl,%dcx_browser_type,32)) set %dcx_browser_type webctrl

  xdid -a $1 4 0 $iif(%dcx_browser_type == webctrl,2,1) Tab %dcx_browser_tabcnt $chr(9) %id panel 0 0 400 300 notheme $chr(9) %url
  xdid -J $1 %id +r arrow
  xdid -E $1 %id + -cdefhmstCM

  xdid -l $1 %id root $chr(9) +pv 0 1 0 0
  xdid -l $1 %id space root $chr(9) + 0 0 0 0
  xdid -l $1 %id cell root $chr(9) +ph 0 0 0 0

  dcx_webbrowser_addtoolbar $1 %id

  xdid -c $1 %id %web_id %dcx_browser_type 0 0 0 0 tabstop

  if (%dcx_browser_type == webctrl) {
    xdid -e $1 %back_id
    xdid -e $1 %forward_id
  }

  xdid -l $1 %id cell root $chr(9) +li %web_id 10 0 0

  dcx_webbrowser_positionaddbutton $1

  dcx_webbrowser_newurl $1 %id %url
}
; $1 = dname, $2 = menu, $3 = sel id
alias -l _unsetstylechecks {
  did -u $1 341-354
  did -c $1 $calc(340 + $3)
  xpop -s $2 3 2 1 $3 $chr(9) +cCrR 1 14
}
; $1 = type
alias -l _setmenustyle {
  var %m = _dcx_webbrowser, %d = dcx_webbrowser
  if ($istok(normal vertical verticalrev button buttonrev grade graderev custom custombig officexp office2003 office2003rev icy icyrev,$1,32)) {
    set %dcx_browser_menustyle $1
    xpopup -l %m 1 default
    xpopup -l %m 7 default
    xpopup -l %m 8 default
    xdialog -P %d -l 1 default
    xdialog -P %d -l 7 default
    xdialog -P %d -l 8 default
    if ($1 == normal) {
      xpopup -t %m normal
      xpopup -l %m 1 $rgb(240,240,240)
      xpopup -l %m 7 $color(83)
      xpopup -l %m 8 $color(59)
      xdialog -P %d -t normal
      xdialog -P %d -l 1 $rgb(240,240,240)
      xdialog -P %d -l 7 $color(83)
      xdialog -P %d -l 8 $color(59)
      _unsetstylechecks %d %m 1
    }
    elseif ($1 == vertical) {
      xpopup -t %m vertical
      xdialog -P %d -t vertical
      _unsetstylechecks %d %m 2
    }
    elseif ($1 == verticalrev) {
      xpopup -t %m verticalrev
      xdialog -P %d -t verticalrev
      _unsetstylechecks %d %m 3
    }
    elseif ($1 == button) {
      xpopup -t %m button
      xpopup -l %m 7 $rgb(13,180,242)

      xdialog -P %d -t button
      xdialog -P %d -l 7 $rgb(13,180,242)
      _unsetstylechecks %d %m 4
    }
    elseif ($1 == buttonrev) {
      xpopup -t %m buttonrev
      xpopup -l %m 7 $rgb(13,180,242)

      xdialog -P %d -t buttonrev
      xdialog -P %d -l 7 $rgb(13,180,242)
      _unsetstylechecks %d %m 5
    }
    elseif ($1 == grade) {
      xpopup -t %m grade
      xpopup -l %m 1 $rgb(255,192,34)

      xdialog -P %d -t grade
      xdialog -P %d -l 1 $rgb(255,192,34)
      _unsetstylechecks %d %m 6
    }
    elseif ($1 == graderev) {
      xpopup -t %m graderev
      xdialog -P %d -t %m graderev
      _unsetstylechecks %d %m 7
    }
    elseif ($1 == custom) {
      xpopup -t %m custom
      var %f = $mircdirtop_bg.jpg
      if ($isfile(%f)) xpopup -b %m %f

      xdialog -P %d -t custom
      if ($isfile(%f)) xdialog -P %d -b %f
      _unsetstylechecks %d %m 8
    }
    elseif ($1 == custombig) {
      xpopup -t %m custombig
      var %f = $mircdirtop_bg.jpg
      if ($isfile(%f)) xpopup -b %m %f
      ; NB: this colour is only seen when alpha draw code is enabled in DrawMenuBitmap() when compiling dll.
      xpopup -l %m 1 $rgb(255,192,34)

      xdialog -P %d -t custombig
      if ($isfile(%f)) xdialog -P %d -b %f
      ; NB: this colour is only seen when alpha draw code is enabled in DrawMenuBitmap() when compiling dll.
      xdialog -P %d -l 1 $rgb(255,192,34)
      _unsetstylechecks %d %m 9
    }
    elseif ($1 == officexp) {
      xpopup -t %m officexp
      xdialog -P %d -t officexp
      _unsetstylechecks %d %m 10
    }
    elseif ($1 == office2003) {
      xpopup -t %m office2003
      xdialog -P %d -t office2003
      _unsetstylechecks %d %m 11
    }
    elseif ($1 == office2003rev) {
      xpopup -t %m office2003rev
      xdialog -P %d -t office2003rev
      _unsetstylechecks %d %m 12
    }
    elseif ($1 == icy) {
      xpopup -t %m icy
      xdialog -P %d -t icy
      _unsetstylechecks %d %m 13
    }
    elseif ($1 == icyrev) {
      xpopup -t %m icyrev
      xdialog -P %d -t icyrev
      _unsetstylechecks %d %m 14
    }
  }
  elseif ($1 == roundsel) {
    xpopup -R %m +r 1
    xdialog -P %d -R +r 1
    ;did -u %d 357
    ;did -c %d 355
  }
  elseif ($1 == roundwin) {
    xpopup -R %m +R 1
    xdialog -P %d -R +R 1
    ;did -u %d 358
    ;did -c %d 356
  }
  elseif ($1 == squaresel) {
    xpopup -R %m +r 0
    xdialog -P %d -R +r 0
    ;did -u %d 355
    ;did -c %d 357
  }
  elseif ($1 == squarewin) {
    xpopup -R %m +R 0
    xdialog -P %d -R +R 0
    ;did -u %d 356
    ;did -c %d 358
  }
  elseif ($1 == tooltips) {
    xpopup -R %m +t 1
    xdialog -P %d -R +t 1
  }
  elseif ($1 == notooltips) {
    xpopup -R %m +t 0
    xdialog -P %d -R +t 0
  }
  elseif ($1 == alpha) {
    xpopup -R %m +a 192
    xdialog -P %d -R +a 192
  }
  elseif ($1 == noalpha) {
    xpopup -R %m +a 255
    xdialog -P %d -R +a 255
  }
}
; $1 = dialog, $2 = menu
alias -l _setweb2ctrl {
  did -u $1 311
  did -c $1 312
  set %dcx_browser_default_type web2ctrl
  xpop -s $2 3 1 2 $chr(9) +cCr 1 2
}
; $1 = dialog, $2 = menu
alias -l _setwebctrl {
  did -u $1 312
  did -c $1 311
  set %dcx_browser_default_type webctrl
  xpop -s $2 3 1 1 $chr(9) +cCr 1 2
}
; $1 = menuname, $2 = id, $3 = cmd
alias dcx_webbrowser_menuevent {
  var %m = $1, %d = $right($1,-1)
  echo -s menu: $1 :2: $2 :3-: $3- :m: %m :d: %d
  if ($2 == 1) {
    ; add ie tab
    set %dcx_browser_type webctrl
    dcx_webbrowser_addtab %d
  }
  elseif ($2 == 2) {
    ;add webview2 tab
    set %dcx_browser_type web2ctrl
    dcx_webbrowser_addtab %d
  }
  elseif ($2 == 311) _setwebctrl %d %m
  elseif ($2 == 312) _setweb2ctrl %d %m
  elseif ($2 == 32) xdialog -p %d +v 1
  elseif ($2 == 33) xdialog -p %d +v 0
  elseif ($2 == 341) _setmenustyle normal
  elseif ($2 == 342) _setmenustyle vertical
  elseif ($2 == 343) _setmenustyle verticalrev
  elseif ($2 == 344) _setmenustyle button
  elseif ($2 == 345) _setmenustyle buttonrev
  elseif ($2 == 346) _setmenustyle grade
  elseif ($2 == 347) _setmenustyle graderev
  elseif ($2 == 348) _setmenustyle custom
  elseif ($2 == 349) _setmenustyle custombig
  elseif ($2 == 350) _setmenustyle officexp
  elseif ($2 == 351) _setmenustyle office2003
  elseif ($2 == 352) _setmenustyle office2003rev
  elseif ($2 == 353) _setmenustyle icy
  elseif ($2 == 354) _setmenustyle icyrev
  elseif ($2 == 355) _setmenustyle roundsel
  elseif ($2 == 356) _setmenustyle roundwin
  elseif ($2 == 357) _setmenustyle squaresel
  elseif ($2 == 358) _setmenustyle squarewin
  elseif ($2 == 359) _setmenustyle tooltips
  elseif ($2 == 360) _setmenustyle notooltips
  elseif ($2 == 361) _setmenustyle alpha
  elseif ($2 == 362) _setmenustyle noalpha
  elseif ($2 == 800) {
    ; load
    var %tabid = $xdialog(%d,$xdid(%d,4,$xdid(%d,4).sel).childid).namedid
    var %url = $sfile($mircdir,.Select a file,Load)
    if (%url != $null) dcx_webbrowser_newurl %d %tabid %url
  }
  elseif ($2 == 803) {
    ; capture
    var %tabid = $xdialog(%d,$xdid(%d,4,$xdid(%d,4).sel).childid).namedid, %d = $mircdirImages
    if ($isdir(%d)) xdid -C %d $+(%tabid,_web) + $+(%d,\ScreenShot_,$ticks,.png)
    else xdid -C %d $+(%tabid,_web) +
  }
  if (($3 == checksel) && (($2 isnum 311-312) || ($2 isnum 341-362))) return $true
}
on *:DIALOG:dcx_webbrowser:menu:*: dcx_webbrowser_menuevent _dcx_webbrowser $did dialog
;alias dcx_webbrowser_menuCallback {
;  ;echo -s menu: $1 :: $2 :: $3-
;  if ($3 == checksel) {
;    if ($2 isnum 311-312) {
;      if ($2 == 311) _setwebctrl dcx_webbrowser $1
;      elseif ($2 == 312) _setweb2ctrl dcx_webbrowser $1
;      return $true
;    }
;  }
;}
on *:DIALOG:dcx_webbrowser:init:0:{
  set %dcx_browser_tabcnt 0

  set %dcx_webbrowser_icon_ie C:\Program Files (x86)\Internet Explorer\iexplore.exe
  var %d = C:\Program Files (x86)\Microsoft\EdgeWebView\Application\, %f = SmallLogo.png
  if ($findfile(%d,%f,1)) set %dcx_webbrowser_icon_webview $v1
  else set %dcx_webbrowser_icon_webview %dcx_webbrowser_icon_ie
  if (!$istok(normal vertical verticalrev button buttonrev grade graderev custom custombig officexp office2003 office2003rev icy icyrev,%dcx_browser_menustyle,32)) set %dcx_browser_menustyle normal

  dcx Mark $dname dcx_webbrowser_work
  xdialog -b $dname +tyz
  xdialog -g $dname +b $rgb(100,10,17)
  xdialog -T $dname +pb
  xdialog -E $dname + -dfhmtM

  xdialog -P $dname
  xdialog -P $dname -R +r 1
  xdialog -P $dname -R +t 1
  xdialog -P $dname -R +a 192
  xdialog -P $dname -l 11 $rgb(255,0,0)
  var %f = Canada.ico
  if ($isfile(%f)) xdialog -P $dname -i + 0 %f
  xdialog -P $dname -x +D

  if (!$istok(webctrl web2ctrl,%dcx_browser_default_type,32)) set %dcx_browser_default_type webctrl

  did -c $dname $iif(%dcx_browser_default_type == webctrl,311,312)

  var %m = _dcx_webbrowser

  ;xpopup -c %m normal $chr(9) dcx_webbrowser_menuCallback
  xpopup -c %m normal $chr(9) dcx_webbrowser_menuevent
  if ($isfile(%dcx_webbrowser_icon_webview)) xpopup -i %m +P 0 %dcx_webbrowser_icon_webview
  if ($isfile(%dcx_webbrowser_icon_ie)) xpopup -i %m + 1 %dcx_webbrowser_icon_ie
  xpop -a %m -1 $chr(9) + 1 2 Add IE Tab $chr(9) Open new IE tab.
  xpop -a %m -1 $chr(9) + 2 1 Add WebView2 Tab $chr(9) Open new WebView2 tab.
  xpop -a %m -1 $chr(9) +s 30 0 Options
  xpop -a %m 3 -1 $chr(9) +s 31 0 Default Browser
  xpop -a %m 3 1 -1 $chr(9) $iif(%dcx_browser_default_type == webctrl,+Ccr,+Cr) 311 0 IE $chr(9) Set Internet Explorer as the default.
  xpop -a %m 3 1 -1 $chr(9) $iif(%dcx_browser_default_type == web2ctrl,+Ccr,+Cr) 312 0 WebView2 $chr(9) Set WebView2 as the default.
  xpop -a %m 3 -1 $chr(9) +s 34 0 Menu Options
  xpop -a %m 3 2 -1 $chr(9) +s 35 0 Menu Styles
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 341 0 Normal $chr(9) Normal looking menu
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 342 0 Vertical $chr(9) Vertical Gradient
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 343 0 VerticalRev $chr(9) Reverse Vertical Gradient
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 344 0 Button $chr(9) Items are buttons
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 345 0 ButtonRev $chr(9) Items are sunken buttons
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 346 0 Gradient $chr(9) Gradient
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 347 0 GradientRev $chr(9) Reverse Gradient
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 348 0 Custom $chr(9) Each item is a custom bitmap
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 349 0 CustomBig $chr(9) Custom bitmap covers whole menu
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 350 0 OfficeXP $chr(9) OfficeXP style
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 351 0 Office2003 $chr(9) Office2003 style
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 352 0 Office2003rev $chr(9) Reverse Office2003 style
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 353 0 Icy $chr(9) Ice effect
  xpop -a %m 3 2 1 -1 $chr(9) +Cr 354 0 IcyRev $chr(9) Reverse Ice effect
  xpop -a %m 3 2 -1 $chr(9) + 355 0 Rounded Selector $chr(9) Makes menu selection a rounded rect
  xpop -a %m 3 2 -1 $chr(9) + 356 0 Rounded Window $chr(9) Make menus rounded rects
  xpop -a %m 3 2 -1 $chr(9) + 357 0 Square Selector $chr(9) Makes menu selection a rounded rect
  xpop -a %m 3 2 -1 $chr(9) + 358 0 Square Window $chr(9) Make menus rects
  xpop -a %m 3 2 -1 $chr(9) + 359 0 Tooltips $chr(9) Enable tooltips like this one!
  xpop -a %m 3 2 -1 $chr(9) + 360 0 No Tooltips $chr(9) Disable tooltips like this one!
  xpop -a %m 3 2 -1 $chr(9) + 361 0 Alpha Blend $chr(9) Set the menus to be translucent
  xpop -a %m 3 2 -1 $chr(9) + 362 0 No Alpha Blend $chr(9) disable alpha blending

  xpop -a %m 3 -1 $chr(9) + 32 0 Show Menubar $chr(9) Show windows menubar.
  xpop -a %m 3 -1 $chr(9) + 33 0 Hide Menubar $chr(9) Hide windows menubar.

  xpopup -R %m +r 1
  xpopup -R %m +t 1
  xpopup -R %m +a 192

  _setmenustyle %dcx_browser_menustyle

  xdialog -c $dname 1 divider 20 20 460 360 notheme vertical
  xdid -x $dname 1 +
  xdid -W $dname 1 3
  xdid -Q $dname 1 $rgb(20,20,25) $rgb(255,150,0)
  xdid -C $dname 1 +b $rgb(0,255,0)

  xdid -l $dname 1 50 0 $chr(9) 2 treeview 0 0 0 0 notheme showsel tabstop
  xdid -r $dname 1 100 0 $chr(9) 3 panel 0 0 0 0 notheme
  xdid -C $dname 3 +b $rgb(255,25,25)
  xdid -E $dname 1,3 + -ces
  xdid -E $dname 2 + -es

  xdid -c $dname 3 4 tab 0 0 0 0 notheme closable gradient tooltips tabstop shadow hottrack transparent
  xdid -C $dname 4 +bk $rgb(0,233,192)
  xdid -C $dname 4 +t $rgb(0,255,0)
  xdid -E $dname 4 + -s
  if ($isfile(%dcx_webbrowser_icon_webview)) xdid -w $dname 4 +P 0 %dcx_webbrowser_icon_webview
  if ($isfile(%dcx_webbrowser_icon_ie)) xdid -w $dname 4 + 1 %dcx_webbrowser_icon_ie

  xdid -J $dname 3,4 +r arrow

  xdid -c $dname 3 5 button 10 0 30 20 transparent noformat bitmap tooltips
  xdid -t $dname 5 +
  xdid -c $dname 5 +h $rgb(0,255,0)
  xdid -m $dname 5 1
  xdid -T $dname 5 Add a tab...

  xdialog -c $dname 24 statusbar 0 0 0 20 tooltips
  xdid -l $dname 24 40% -1
  var %f = $mircdirCanada.ico
  if ($isfile(%f)) xdid -w $dname 24 + 0 %f
  xdid -t $dname 24 1 +c 0 25 pbar 0 0 100 20 tooltips gradient transparent shadow noformat
  xdid -c $dname 25 16776960
  xdid -v $dname 25 0
  xdid -j $dname 25 a
  xdid -T $dname 25 ProgressBar ToolTip!
  xdid -t $dname 24 2 + 1 ... $chr(9) ...
  xdid -E $dname 24,25 + -es

  xdid -l $dname 3 root $chr(9) +pv 0 1 0 0
  xdid -l $dname 3 space root $chr(9) + 0 0 0 0
  xdid -l $dname 3 cell root $chr(9) +li 4 1 0 0

  xdialog -l $dname root $chr(9) +pv 0 1 0 0
  xdialog -l $dname space root $chr(9) + 5 5 5 22
  xdialog -l $dname cell root $chr(9) +li 1 1 0 0

  var %url = https://www.google.com/
  xdid -a $dname 2 $tab(-1,+ec 1 2 1 1 0 $rgb(0,0,255) 0 %url, %url)
  var %url = https://www.youtube.com/
  xdid -a $dname 2 $tab(-1,+ec 1 2 1 1 0 $rgb(0,0,255) 0 %url, %url)
  var %url = https://www.bing.com/
  xdid -a $dname 2 $tab(-1,+ec 1 2 1 1 0 $rgb(0,0,255) 0 %url, %url)
  var %url = https://aminet.net/
  xdid -a $dname 2 $tab(-1,+ec 1 2 1 1 0 $rgb(0,0,255) 0 %url, %url)

  set %dcx_browser_type %dcx_browser_default_type
  dcx_webbrowser_addtab $dname

  .timer 1 0 dcx_webbrowser_postinit $dname
}
; $1 = dname, $2 = tab id, $3- filename
alias -l _setfavicon {
  if (!$dialog($1)) return
  if (!$isfile($3-)) return
  var %id = $+($2,_favicon)
  xdid -s $1 %id
  xdid -i $1 %id +ghab $3-
}
; $1 = dname
alias -l _resetprogress {
  if (!$dialog($1)) return
  xdid -i $1 25 Waiting...
}
alias dcx_webbrowser_work {
  !if ($2 == error) echo -s DCXError: $1 ID: $3 Type: $4 Prop: $5 Cmd: $6 Error: $7-
  !else echo -s dname: $1 :event: $2 :id: $3 :args: $4-

  if ($2 == sclick) {
    if ($3 == 2) {
      ; treeview control
      if ($4- != $null) echo -s treeview item path: $v1 treeview item text: $xdid($1,$3,$4-).text
    }
    elseif ($3 == 4) .timer 1 1 dcx_webbrowser_positionaddbutton $1
    elseif ($3 == 5) {
      set %dcx_browser_type %dcx_browser_default_type
      dcx_webbrowser_addtab $1
    }
    else {
      var %id = $xdialog($1,$3).namedid
      if (Tab*_back iswm %id) xdid -k $1 $+($gettok(%id,1,95),_web)
      elseif (Tab*_forward iswm %id) xdid -i $1 $+($gettok(%id,1,95),_web)
      elseif (Tab*_refresh iswm %id) xdid -r $1 $+($gettok(%id,1,95),_web)
      elseif (Tab*_home iswm %id) dcx_webbrowser_gohome $1 $gettok(%id,1,95)
    }
  }
  elseif ($2 == dclick) {
    if ($3 == 2) {
      ; treeview control
      if ($4- != $null) {
        ;echo -s treeview item path: $v1 treeview item text: $xdid($1,$3,$4-).text
        var %url = $xdid($1,$3,$4-).text
        if (%url != $null) {
          var %id = $xdialog($1,$xdid($1,4,$xdid($1,4).sel).childid).namedid
          dcx_webbrowser_newurl $1 %id %url
        }
      }
    }
  }
  elseif ($2 == rclick) {
    if (($3 == 2) || ($3 == 4) || ($3 == 5)) {
      xpopup -s _dcx_webbrowser + $xdialog($1).mouse $dialog($1).hwnd

      return $false
    }
  }
  elseif ($2 == closetab) dcx_webbrowser_remtab $1 $4
  elseif ($2 == title) {
    ; title text
    var %id = $gettok($xdialog($1,$3).namedid,1,95), %cnt = 1, %total = $xdid($1,4).num
    while (%cnt < %total) {
      if (%id == $xdialog($1,$xdid($1,4,%cnt).childid).namedid) break
      inc %cnt
    }
    xdid -t $1 4 %cnt $left($4-,40) $+ $iif($len($4-) > 40,...)
    dcx_webbrowser_positionaddbutton $1
  }
  elseif ($2 == endsize) {
    .timer 1 0 dcx_webbrowser_positionaddbutton $1
  }
  elseif ($2 == scroll) {
    if ($3 == 4) .timer 1 0 dcx_webbrowser_positionaddbutton $1
  }
  elseif ($2 == dleave) xdid -t $1 24 2 + 1 - $chr(9) -
  elseif ($2 == status) xdid -t $1 24 2 + 1 $4- $chr(9) $4-
  elseif ($2 == doc_complete) xdid -t $1 24 2 + 1 Done $chr(9) ...
  elseif ($2 == source_changed) {
    ; url
    var %id = $xdialog($1,$3).namedid
    xdid -ra $1 $+($gettok(%id,1,95),_edit) $4-
    xdid -t $1 24 2 + 1 Downloading... $4- $chr(9) $4-
    xdid -h $1 $+($gettok(%id,1,95),_favicon)
    xdid -i $1 $+($gettok(%id,1,95),_favicon) + none
  }
  elseif ($2 == nav_begin) {
    ; url
  }
  elseif ($2 == nav_complete) xdid -t $1 24 2 + 1 Download $iif($4,Complete,Failed) $chr(9) ...
  elseif ($2 == dl_begin) {
    ; size filename
    ; NB: can't use $file($5-).path as this doesnt work when the file doesnt exist.
    var %f = $+($nofile($5-),file.tmp)
    if ($isfile(%f)) return cancel
    .timerresetprogress off
    xdid -v $1 25 0
    if ($0 > 3) {
      xdid -r $1 25 0 $4
      xdid -i $1 25 $nopath(%f) $+(%,d/,$4)
    }
    xdid -t $1 24 2 + 1 Downloading... $chr(9) %f

    return change %f
  }
  elseif ($2 == dl_progress) {
    ; bytes totalbytes filename
    xdid -v $1 25 $4
  }
  elseif ($2 == dl_completed) {
    ; filename
    xdid -v $1 25 0
    xdid -i $1 25 Completed: $4-
    .timerresetprogress 1 5 _resetprogress $1
    xdid -t $1 24 2 + 1 ... $chr(9) ...
  }
  elseif ($2 == dl_canceled) {
    ; reason filename
    xdid -v $1 25 0
    xdid -i $1 25 Canceled: $5-
    .timerresetprogress 1 5 _resetprogress $1
    xdid -t $1 24 2 + 1 ... $chr(9) ...
  }
  elseif ($2 == back) {
    ; cangoback
    var %id = $xdialog($1,$3).namedid
    xdid $iif($4,-e,-b) $1 $+($gettok(%id,1,95),_back)
  }
  elseif ($2 == forward) {
    ; cangoforwards
    var %id = $xdialog($1,$3).namedid
    xdid $iif($4,-e,-b) $1 $+($gettok(%id,1,95),_forward)
  }
  elseif ($2 == history) {
    ; cangoback cangoforwards
    var %id = $xdialog($1,$3).namedid
    xdid $iif($4,-e,-b) $1 $+($gettok(%id,1,95),_back)
    xdid $iif($5,-e,-b) $1 $+($gettok(%id,1,95),_forward)
  }
  elseif ($2 == win_open) {
    ; url
    if (*bing.com* iswm $4-) return cancel
  }
  elseif ($2 == favicon) {
    ; changed url
    ; saved filename
    var %id = $xdialog($1,$3).namedid
    if ($4 == saved) .timer 1 0 _setfavicon $1 $gettok(%id,1,95) $5-
    elseif ($4 == changed) {
      var %d = $mircdirImages
      if ($isdir(%d)) {
        var %f = $+(%d,\favicon_,$md5($5-),.png)
        if (!$isfile(%f)) return save %f
        else .timer 1 0 _setfavicon $1 $gettok(%id,1,95) %f
      }
    }
  }
  elseif ($2 == devtools) {
    var %id = $xdialog($1,$3).namedid
    if ($4 == securityState=secure) xdid -s $1 $+($gettok(%id,1,95),_security)
    else xdid -h $1 $+($gettok(%id,1,95),_security)
  }
  elseif (($2 == keyup) && ($4 == 13)) {
    var %id = $xdialog($1,$3).namedid
    if (Tab*_edit iswm %id) {
      var %url = $xdid($1,%id).text
      if (%url != $null) dcx_webbrowser_newurl $1 $gettok(%id,1,95) %url
    }
  }
  elseif ($2 == close) {
    xpopup -d _dcx_webbrowser
  }

}
ON *:SIGNAL:XPopup-_dcx_webbrowser: {
  ;// nothing clicked
  if ($1 == 0) return

  dcx_webbrowser_menuevent _dcx_webbrowser $1 signal
}
